plugins {
	id 'org.springframework.boot' version '2.7.1'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'

	//jacoco
	id 'jacoco'
}

group = 'com.psc'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

//jacoco 버전 명시
jacoco {
	toolVersion = '0.8.6'
}

jar {
	enabled = false
}


jacocoTestReport {
	reports {
		html.enabled true
		csv.enabled true
		xml.enabled false
	}
	finalizedBy 'jacocoTestCoverageVerification'
}


dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'mysql:mysql-connector-java'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
	//test - lombok
	testCompileOnly 'org.projectlombok:lombok:1.18.12'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			// 클래스 단위로 테스트 커버리지 측정
			element = 'CLASS'

			// 분기 커버리지 100%
			limit {
				counter = 'BRANCH'
				value = 'COVEREDRATIO'
				minimum = 0
			}

			// 문장 커버리지 80%
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0
			}

			// 스프링 부트 메인 메소드 제외
			excludes = ['com.psc.j211.J211Application']
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy 'jacocoTestReport'
}
